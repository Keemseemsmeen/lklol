//============================================================================================================================================
//
//============================================================================================================================================
private["_vehicle","_shooter","_ammo", "_missile","_event","_data","_camView","_cam"];
if ( !GZL_MISSILE_ACTIVE ) then {
//============================================================================================================================================
// Assign parameters
//============================================================================================================================================
	_event = _this select 0; 
	_data = _this select 1; 
//============================================================================================================================================
// Init variables
//============================================================================================================================================
	_vehicle = objNull;
	_shooter = objNull;
	_missile = objNull;
	_ammo = "";	
	_camView = cameraView;
	
	switch ( _event ) do {
		case "INCOMING" : {			
			_vehicle = _data select 0; 
			_ammo = _data select 1;
			_shooter = _data select 2;
			_missile = nearestObject [_shooter, _ammo];};		
		case "FIRED" : {			
			_vehicle = _data select 0;
			_ammo = _data select 4;
			_missile = nearestObject [_vehicle, _ammo];			
			if ( !(_missile isKindOf "MissileBase") )
			then { _missile = objNull;}
			else { _vehicle switchCamera "EXTERNAL"; sleep 0.6  * acctime;};};};
//============================================================================================================================================
// Setup Camera
//============================================================================================================================================	 
	if ( !isNull _missile ) then
	{		
		
		_cam = "camera" camCreate (position _missile);
		_cam camSetTarget _missile; 
		_cam camSetFOV 0.27; 	
		_cam cameraEffect ["INTERNAL", "BACK"];
		"RadialBlur" ppEffectEnable true;
		"RadialBlur" ppEffectAdjust [0.01,0.01,0.1,0.1]; 
        "RadialBlur" ppEffectCommit 0.05; 
		GZL_MISSILE_ACTIVE = true;
//============================================================================================================================================
// Main Loop
//============================================================================================================================================
		while { !isNull _missile } do {			
			_cam camSetRelPos [0,-2,0.15];
			_cam camCommit 0;
			
			if ( GZL_SCENE_SKIP || GZL_SCENE_STOP ) exitWith{};			
			sleep GZL_SLEEP * GZL_ACCTIME;
		};
//============================================================================================================================================
// Cleanup
//============================================================================================================================================
		"RadialBlur" ppEffectEnable false;
		sleep 1  * acctime;		
		//_cam camSetFOV 0.9;
		// _cam camSetTarget objNull;
		// _cam camCommit 0;		
		_vehicle switchCamera _camView;
		//_cam cameraEffect ["TERMINATE", "BACK"];
		camDestroy _cam;		
	};
	
	GZL_MISSILE_ACTIVE = false;
};
//============================================================================================================================================
//
//============================================================================================================================================
